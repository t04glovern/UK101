10 PRINT TAB(30); "MASTERMIND"
20 PRINT TAB(30); "=========="
30 DIM A$(72), B$(6), X$(72), Y$(4), Z$(4)
40 LET B$ = "BGOPRY"
50 PRINT "TYPE IN ANY NUMBER BETWEEN 1 AND 1000."
60 INPUT A
70 PRINT "THE CODE GROUP CONSISTS OF A COMBINATION OF 4 COLOURS CHOSEN"
80 PRINT "FROM THE FOLLOWING: BLUE (B), GREEN (G), ORANGE (O), RED (R),"
90 PRINT "PURPLE (P) AND YELLOW (Y)."
100 PRINT "FOR EACH GUESS THAT IS CORRECT IN POSITION AS WELL AS COLOUR"
110 PRINT "YOU WILL BE AWARDED ONE POINT UNDER 'BLACK', AND FOR EACH"
120 PRINT "GUESS THAT IS CORRECT IN COLOUR ONLY, YOU WILL RECEIVE ONE"
130 PRINT "POINT UNDER 'WHITE'."
140 PRINT "YOU HAVE 7 ATTEMPTS."
150 FOR N = 1 TO A
    160 LET Y = RND(1)
170 NEXT N
180 FOR N = 1 TO 4
    190 LET Y = INT(6 * RND(1)) + 1
    200 LET Y$(N) = B$(Y)
    210 LET Z$(N) = B$(Y)
220 NEXT N
230 PRINT "B = BLUE", "G = GREEN"
240 PRINT "O = ORANGE", "R = RED"
250 PRINT "P = PURPLE", "Y = YELLOW"
260 PRINT "INPUTS", "BLACK", "WHITE"
270 PRINT "------", "-----", "-----"
280 FOR P = 1 TO 7
    290 INPUT X$
    300 IF LEN(X$) <> 4 THEN 350
    310 FOR N = 1 TO 4
        320 FOR M = 1 TO 6
            330 IF X$(N) = B$(M) THEN 370
        340 NEXT M
        350 PRINT "NOT ALLOWED. TYPE IN 4 LETTERS CHOSEN FROM: B, G, O, P, R, Y"
        360 GOTO 290
    370 NEXT N
    380 IF X$ = Z$ THEN 590
    390 LET W = B = 0
    400 FOR N = 1 TO 4
        410 IF X$(N) <> Z$(N) THEN 450
        420 LET B = B + 1
        430 LET X$(N) = "?"
        440 LET Z$(N) = "#"
    450 NEXT N
    460 FOR N = 1 TO 4
        470 FOR M = 1 TO 4
            480 IF X$(N) <> Z$(M) THEN 520
            490 LET W = W + 1
            500 LET Z$(M) = " "
            510 GOTO 530
        520 NEXT M
    530 NEXT N
    540 LET Z$ = Y$
    550 PRINT TAB(16); B; TAB(31); W
560 NEXT P
570 PRINT "YOU LOSE. THE CORRECT SOLUTION IS:  "; Y$
580 GOTO 600
590 PRINT "JWELL DONE. THAT IS CORRECT."
600 PRINT "DO YOU WISH TO PLAY AGAIN?"
610 INPUT A$
620 IF A$ = "NO" THEN 660
630 IF A$ = "YES" THEN 180
640 PRINT "NOT UNDERSTOOD. TYPE IN EITHER 'YES' OR 'NO'."
650 GOTO 610
660 PRINT "THANK YOU FOR THE GAME. GOODBYE."
670 END
